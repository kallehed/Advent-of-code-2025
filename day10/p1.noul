total_presses := 0;
for (l <- read().lines) (
  to_get_str, r := l drop 1 split "]";
  bits := to_get_str.len;
  to_get := to_get_str map (=="#") then enumerate map (\[i,v] -> 2^i * v) then sum;
  buttons_str, _ := r split "{";
  buttons_idx := buttons_str.trim split " " map \b -> b[1:-1] split "," map int;
  # turn them into bool-vector, that can be added later
  button_nums := buttons_idx map (_ map (2^) then sum);
  # be greedy, look for combinations which create 'to_get', start with smallest combinations (fewest presses)
  for (nr_presses <- 1 to bits)
    for (c <- button_nums combinations nr_presses)
      if ((c fold xor) == to_get) (
        total_presses += nr_presses;
        break break continue; # AMAZING FEATURE!!!!!!!!
      );
);
print("TOTAL: ", total_presses);
